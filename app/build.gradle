apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'


// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

def supportLibraryVersion = "27.0.2"
def googlePlayServicesVersion = "11.8.0"


android {
    signingConfigs {
        config {
            storeFile file(keystoreProperties['STORE_FILE'])
            storePassword keystoreProperties['KEYSTORE_PASSWORD']
            keyAlias keystoreProperties['KEY_ALIAS']
            keyPassword keystoreProperties['KEY_PASSWORD']
        }
    }
    compileSdkVersion 27
    buildToolsVersion '27.0.2'
    defaultConfig {
        applicationId "com.malcolm.portsmouthunibus"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 25
        versionName '1.0.4.1'
        vectorDrawables.useSupportLibrary = true
        signingConfig signingConfigs.config
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            it.buildConfigField 'String', 'API_KEY', APIKEYRELEASE
            signingConfig signingConfigs.config
            manifestPlaceholders = [MAPS_API_KEY     : keystoreProperties['MAPS_RELEASE_API_KEY'],
                                    AWARENESS_API_KEY: keystoreProperties['AWARENESS_RELEASE_API_KEY'],
                                    CRASHLYTICS_API_KEY: keystoreProperties['CRASHLYTICS_API_KEY']]
        }
        debug {
            it.buildConfigField 'String', 'API_KEY', APIKEYDEBUG
            manifestPlaceholders = [MAPS_API_KEY     : keystoreProperties['MAPS_DEBUG_API_KEY'],
                                    AWARENESS_API_KEY: keystoreProperties['AWARENESS_DEBUG_API_KEY'],
                                    CRASHLYTICS_API_KEY: keystoreProperties['CRASHLYTICS_API_KEY']]
            applicationIdSuffix ".debug"
        }
    }
    packagingOptions {

    }
    productFlavors {
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //compile 'com.google.android.gms:play-services-awareness:10.2.1'
    implementation project(':utilities')
    implementation "com.android.support:appcompat-v7:${supportLibraryVersion}"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation "com.android.support:cardview-v7:${supportLibraryVersion}"
    implementation "com.android.support:recyclerview-v7:${supportLibraryVersion}"
    implementation "com.android.support:support-fragment:${supportLibraryVersion}"
    implementation "com.android.support:design:${supportLibraryVersion}"
    implementation "com.android.support:support-vector-drawable:${supportLibraryVersion}"
    implementation "com.android.support:preference-v14:${supportLibraryVersion}"
    implementation "com.android.support:palette-v7:${supportLibraryVersion}"
    implementation "com.google.android.gms:play-services-location:${googlePlayServicesVersion}"
    implementation "com.google.android.gms:play-services-maps:${googlePlayServicesVersion}"
    implementation 'com.google.maps.android:android-maps-utils:0.4.4'
    implementation "com.google.firebase:firebase-core:${googlePlayServicesVersion}"
    implementation 'com.roughike:bottom-bar:2.3.1'
    implementation 'com.getkeepsafe.taptargetview:taptargetview:1.9.1'
    implementation 'com.github.daniel-stoneuk:material-about-library:2.2.1'
    implementation 'com.jakewharton:butterknife:8.5.1'
    implementation 'com.airbnb.android:epoxy:2.5.0'
    implementation 'com.airbnb.android:lottie:2.1.2'
    implementation 'com.squareup.retrofit2:retrofit:2.1.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.1.0'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.pacioianu.david:ink-page-indicator:1.2.0'
    testImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.3'
    testImplementation 'junit:junit:4.12'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
    annotationProcessor 'com.airbnb.android:epoxy-processor:2.5.0'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.3'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.3'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    //compile "com.google.dagger:dagger:2.9"
    //annotationProcessor "com.google.dagger:dagger-compiler:2.9"
    compileOnly 'javax.annotation:jsr250-api:1.0'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.1@aar') {
        transitive = true
    }
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.firebase-crash'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'io.fabric'



